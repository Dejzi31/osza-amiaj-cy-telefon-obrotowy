apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply from: '../gradle/dependencies.gradle'

android {
    compileSdkVersion 28
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 23
        targetSdkVersion 28

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
}

configurations {
    ktlint
}

dependencies {
    ktlint "com.pinterest:ktlint:${otherVersions.ktlint}"

    kapt(
        "androidx.room:room-compiler:${versions.room}"
    )

    kaptAndroidTest("androidx.room:room-compiler:${versions.room}")

    implementation(
        "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${versions.kotlin}",

        "io.reactivex.rxjava2:rxjava:${versions.rxJava}",
        "io.reactivex.rxjava2:rxkotlin:${versions.rxKotlin}",

        "androidx.room:room-runtime:${versions.room}",
        "androidx.room:room-rxjava2:${versions.room}",

        project(":libraries:core"),
    )

    testImplementation(
        "junit:junit:${testVersions.junit4}"
    )

    androidTestImplementation(
        "androidx.test:core:${testVersions.androidXTestCore}",
        "androidx.test:runner:${testVersions.androidXTestRunner}",
    )
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}
